apply plugin: 'groovy'

repositories { jcenter() }

dependencies {
    compileOnly gradleApi()
    // Checkstyle
    compile('com.puppycrawl.tools:checkstyle:8.10.1') {
        exclude module: "guava"
        exclude module: "commons-logging"
    }
    // PMD
    compile('net.sourceforge.pmd:pmd-java:6.4.0') {
        exclude module: "asm"
        exclude module: "gson"
    }
    // The following dependencies need to be respected with regards to the version that lint brings of them
    compileOnly 'com.google.guava:guava:22.0'
    compileOnly 'commons-logging:commons-logging:1.1.1'
    compileOnly 'org.ow2.asm:asm:5.1'
    compileOnly 'com.google.code.gson:gson:2.7'
}

def masterCommitCount = 'git rev-list --count HEAD'.execute([], project.rootDir).text.trim()
if (masterCommitCount == "") { masterCommitCount = "0" }
if (masterCommitCount == "50") { masterCommitCount = "99999" } // Dirty hack to make mavenLocal gain prio on Travis
masterCommitCount = Integer.parseInt(masterCommitCount) + 62

project.ext {
    plugin = 'org.stoyicker.android-check'
    name = 'Android Check Plugin 2'
    description = 'Static code analysis plugin for Android projects.'
    groupId = 'org.stoyicker.android-check'
    artifactId = 'plugin'
    version = "2.$masterCommitCount"
    website = 'https://github.com/stoyicker/android-check-2'
    scm = 'https://github.com/stoyicker/android-check-2'
    tags = ['android', 'plugin', 'check', 'checkstyle', 'pmd']
    pom = {
        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }
        scm {
            url project.ext.scm
            connection 'scm:git@github.com:stoyicker/android-check-2.git'
            developerConnection 'scm:git@github.com:stoyicker/android-check.git'
        }
        issueManagement {
            system 'GitHub'
            url "${project.ext.scm}/issues"
        }
        developers {
            developer {
                name 'Jorge Antonio Diaz-Benito Soriano'
                url 'https://www.linkedin.com/in/jorgediazbenitosoriano'
                roles {
                    role 'developer'
                }
            }
        }
    }
    bintray = {
        user = project.hasProperty('bintray.user') ? project['bintray.user'] : 'anonymous'
        key = project.hasProperty('bintray.key') ? project['bintray.key'] : 'api-key'
        repo = 'android-check-2'
        org = 'stoyicker-org'
        licenses = ['MIT']
        issueTracker = 'https://github.com/stoyicker/android-check-2/issues'
        vcs = project.ext.scm
        sign = true
        dryRun = false
        publish = true
    }
}

apply from: project.file('ci/gradle-publish.gradle')
apply from: project.file('ci/publish.gradle')
